import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'idea'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id 'com.modrinth.minotaur' version '2.+'
}

archivesBaseName = "${mod_id}-fabric-${fabric_supported_versions}"

dependencies {
    minecraft "com.mojang:minecraft:${fabric_minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")

    // JEI
    if (project.hasProperty('jei_version')) {
        modCompileOnly("mezz.jei:jei-${fabric_minecraft_version}-common-api:${jei_version}")
        modCompileOnly("mezz.jei:jei-${fabric_minecraft_version}-fabric-api:${jei_version}")
        modRuntimeOnly("mezz.jei:jei-${fabric_minecraft_version}-fabric:${jei_version}")
    }

    // CONFIGURED
    if (project.hasProperty('fabric_configured_version')) {
        modImplementation "curse.maven:configured-fabric-667378:${fabric_configured_version}"
    }

    // CATALOGUE
    if (project.hasProperty('fabric_catalogue_version')) {
        modImplementation("curse.maven:catalogue-667377:${fabric_catalogue_version}")
        if (project.hasProperty('fabric_menulogue_version')) {
            modImplementation "curse.maven:menulogue-682371:${fabric_menulogue_version}"
        }
    }

//    modImplementation "curse.maven:applied-energistics-2-223794:4735349" // https://www.curseforge.com/minecraft/mc-mods/applied-energistics-2/files

}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = file('../../API Tokens/ae2qolrecipes.md')
    def projectId = "${cf_id}".toString()
    def mainFile = upload(projectId, file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
    mainFile.changelogType = 'markdown'
    mainFile.changelog = file('changelog.md')
    mainFile.addGameVersion('Client', 'Server')
    mainFile.releaseType = "${release_type}"
    "${fabric_release_versions}".split(',').each {
        mainFile.addGameVersion("${it}")
    }
    mainFile.addRequirement("fabric-api")
    mainFile.addRequirement("applied-energistics-2")
    mainFile.addOptional("catalogue-fabric")
}

modrinth {
    token = file('../../API Tokens/modrinth.md').text
    projectId = "${modrinth_id}"
    versionName = getArchivesBaseName() + "-" + getVersion()
    versionType = "${release_type}"
    changelog = file('changelog.md').text
    uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
    gameVersions = "${fabric_release_versions}".split(",") as List<String>
    dependencies {
        required.project "fabric-api"
        required.project "ae2"
    }
}